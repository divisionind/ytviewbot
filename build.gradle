/*
 * ytviewbot - just a YouTube view bot
 * Copyright (C) 2019 Division Industries LLC
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

import org.gradle.internal.os.OperatingSystem
import org.apache.tools.ant.filters.ReplaceTokens
import java.lang.reflect.Method

plugins {
    id 'java'

    id 'application'

    id "com.github.breadmoirai.github-release" version "2.2.9"
}

def baseVersion = "2019.0.3"
def beta = true
def uploadBetas = false

group 'com.divisionind'
mainClassName = "com.divisionind.ytvb.YTViewBot"
sourceCompatibility = 1.8

// returns hash of the current commit
def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

// returns what commit this is in the entire history of the repo
def getGitCommitNum = { shash ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', shash
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

// gets the changes sense the specified short hash
def getGitChanges = { shash ->
    def stdout = new ByteArrayOutputStream()
    exec { // if this fails, try changing %h and %s to separate arguments
        int lastReleaseInt
        try {
            lastReleaseInt = Integer.parseInt(getGitCommitNum(shash))
        } catch (NumberFormatException e) {
            lastReleaseInt = 0
        }
        commandLine 'git', 'log', '--pretty=format:* %h %s', '--max-count', "${Integer.parseInt(getGitCommitNum('HEAD'))-Integer.parseInt(getGitCommitNum(shash))}"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

// what format the version info should take
version baseVersion + (beta ? "-beta.${getGitCommitNum('HEAD')}" : "")

// must be used after githubRelease, could be used for providing changelog to other destinations than github
def lastRelease

// generates sources by replacing these strings (surrounded by @, e.g. @DivisionVersion@)
task generateSources(type: Copy) {
    from 'src/main/java'
    into "$buildDir/generated-src"

    filter(ReplaceTokens, tokens: [
            'DivisionVersion': project.version,
            'DivisionGitHash': getGitHash(),
            'DivisionGitComm': getGitCommitNum('HEAD')
    ])
}

compileJava.setSource("$buildDir/generated-src")
compileJava.dependsOn generateSources

task makeJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }
    version = ''
    baseName = project.name
    from { configurations.compile.filter{ it.getName().endsWith(".jar") }.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task pack(dependsOn: ['clean', 'makeJar'])
makeJar.mustRunAfter clean

task linux(dependsOn: pack, type: Exec) {
    if (OperatingSystem.current().isLinux()) {
        workingDir 'build/libs'
        commandLine "sh", "../../linux.sh"
    } else println 'Build platform not linux. Cannot run some tasks.'
}

task buildRelease(dependsOn: linux, type: Exec) {
    if (OperatingSystem.current().isLinux()) {
        workingDir 'build'
        commandLine "sh", "../buildTar.sh", "${project.version}"
    } else println 'Build platform not linux. Cannot run some tasks.'
}

githubRelease {
    token "${System.env.GITHUB_TOKEN}"
    owner "divisionind"
    repo "ytviewbot"
    tagName "v${project.version}"
    targetCommitish "master"
    releaseName "v${project.version}"
    draft false
    prerelease beta
    releaseAssets "build/ytviewbot-${project.version}.tar.gz"
    apiEndpoint "https://api.github.com"

    // gets the last released commit from releaseplugin to calculate change log on its own (more control)
    Class c = Class.forName("com.github.breadmoirai.githubreleaseplugin.ChangeLogSupplier")
    Method m = c.getDeclaredMethod("getLastReleaseCommit")
    m.setAccessible(true)
    lastRelease = m.invoke(changelog())

    if (System.env.TRAVIS != null) {
        body "${getGitChanges(lastRelease)}"
    }
}

// a dummy task so that the githubRelease task may be disable based on certain parameters
task uploadArtifacts {}

if (!(beta && !uploadBetas)) {
    println "Version qualified for a release."
    uploadArtifacts.finalizedBy tasks.githubRelease
}

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.seleniumhq.selenium/selenium-java
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'

    // https://mvnrepository.com/artifact/info.picocli/picocli
    compile group: 'info.picocli', name: 'picocli', version: '3.8.2'

    // https://mvnrepository.com/artifact/org.jline/jline
    compile group: 'org.jline', name: 'jline', version: '3.11.0'

    // https://mvnrepository.com/artifact/net.java.dev.jna/jna
    compile group: 'net.java.dev.jna', name: 'jna', version: '5.3.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}